apply plugin: 'com.android.application'

project.ext.set('NAP_ROOT', '../../../NAP-0.2.2-Android')

// This needs to be a list of all dependencies; populated manually. project.json and
// module.json files aren't being processed for dependencies (for now?)
def napModules = ['mod_napscene', 'mod_napdatabase', 'mod_napapp', 'mod_napinput', 'mod_napmath', 'mod_napapi']

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId = 'nl.naivi.emography'
        minSdkVersion 16
        targetSdkVersion 28
        versionCode = 1
        versionName = '1.0'
        externalNativeBuild {
            cmake {
                cppFlags "-frtti -fexceptions -std=c++14"
                arguments '-DANDROID_STL=c++_static',
                          '-DNAP_ROOT=' + file(project.ext.get('NAP_ROOT')).absolutePath,
                          '-DNAP_MODULES=' + napModules.join(';')
            }
        }
        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
//            abiFilters 'arm64-v8a'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                          'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
        }
        release {
            def srcDirs = [project.NAP_ROOT + '/thirdparty/rttr/bin/Release',
                           project.NAP_ROOT + '/lib/Release'
                           ]
            for (String napModule : napModules) {
                srcDirs.add(project.NAP_ROOT + '/modules/' + napModule + '/lib/Release')
            }
            jniLibs.srcDirs = srcDirs

        }
        debug {
            def srcDirs = [project.NAP_ROOT + '/thirdparty/rttr/bin/Debug',
                           project.NAP_ROOT + '/lib/Debug'
            ]
            for (String napModule : napModules) {
                srcDirs.add(project.NAP_ROOT + '/modules/' + napModule + '/lib/Debug')
            }
            jniLibs.srcDirs = srcDirs
        }
    }
    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
}